# download grml config if doesn't exist
if [ ! -f ~/.zsh/grml.zsh ]; then
    echo "<> grml configuration not found"
    echo "<> installing grml zsh configuration"
    curl -L https://git.grml.org/f/grml-etc-core/etc/zsh/zshrc --create-dirs -o ~/.zsh/grml.zsh
fi

# load grml config
. ~/.zsh/grml.zsh

# prompts
autoload -U promptinit; promptinit

if git --version > /dev/null
then
    # two-line prompt with git integration
    # download if needed
    if [ ! -d ~/.zsh/agkozak-zsh-prompt ]; then
        echo "<> agkozak prompt not found"
        echo "<> loading agkozak zsh prompt"
        git clone https://github.com/agkozak/agkozak-zsh-prompt \
            "$HOME/.zsh/agkozak-zsh-prompt"
    fi
    fpath+=($HOME/.zsh/agkozak-zsh-prompt)

    # options
    AGKOZAK_LEFT_PROMPT_ONLY=1
    AGKOZAK_PROMPT_DIRTRIM=0

    # if in ssh, change looks
    if [ -n "$SSH_CONNECTION" ]
    then
        AGKOZAK_COLORS_USER_HOST=magenta
    fi

    # setup
    # promptinit must go after `fpath` modification
    promptinit
    # actually choose the prompt
    prompt agkozak-zsh-prompt
else
    # fallback, comes with zsh by default
    prompt adam2
fi

# globals
local WORK="$HOME/Workspace"
# "main" function, because I do want my local variables to stay local
function () {
    # some variables
    local DOTFILES="$HOME/.config/dotfiles"
    local SHELL_RC="$HOME/.config/zsh/.zshrc"
    local VIM_RC="$HOME/.config/nvim/init.lua"
    export EDITOR="nvim"

    # add ".local/bin" to path for locally installed scripts (like pipx)
    export PATH="$HOME/.local/bin:$PATH"

    # set up ICU4C paths (needed for building PyICU)
    ICU_PATH="$(brew --prefix icu4c)"
    # ICU_BIN_PATH="$ICU_PATH/bin"
    # ICU_SBIN_PATH="$ICU_PATH/sbin"
    ICU_PKG_PATH="$ICU_PATH/lib/pkgconfig"
    # ${var:+smth} means put `smth` if `var` is set, or nothing otherwise
    PKG_CONFIG_PATH="${PKG_CONFIG_PATH:+$PKG_CONFIG_PATH:}"
    PKG_CONFIG_PATH+="$ICU_PKG_PATH"
    export PKG_CONFIG_PATH

    # aliases
    alias cddot="cd $DOTFILES"
    alias upd="source $SHELL_RC"
    alias edsh="$EDITOR $SHELL_RC"
    alias edvim="$EDITOR $VIM_RC"

    # if has python3
    if which python3 > /dev/null 2>&1; then
        # but doesn't have python command
        if ! which python > /dev/null 2>&1; then
            # just add the alias, for the love of god
            alias python="python3"
        fi
    fi

    # activate virtual environment
    alias activate="source .venv/bin/activate"
}

# helpers
cdwork() {
    cd $WORK/$argv[1]
}

_cdwork() {
    _directories -W "$WORK"
}

compdef _cdwork cdwork
